Wit {

  File
    = (PackageDecl ";")? (PackageItem | NestedPackageDefinition)*

  PackageDecl
    = "package" packageIdentifier

  NestedPackageDefinition = PackageDecl "{" InterfaceItem* "}"

  PackageItem
    = ToplevelUseItem
    | InterfaceItem
    | WorldItem

  WorldItem = Gate "world" ident "{" WorldItems* "}"

  WorldItems = Gate WorldDefinition

  WorldDefinition
    = ExportItem
    | ImportItem
    | UseItem
    | TypedefItem
    | IncludeItem

  ExportItem
    = ExportItemExternType
    | ExportItemUsePath

  ExportItemExternType = "export" ident ":" ExternType

  ExportItemUsePath = "export" UsePath ";"

  ImportItem
    = ImportItemExternType
    | ImportItemUsePath

  ImportItemExternType = "import" ident ":" ExternType

  ImportItemUsePath = "import" UsePath ";"

  IncludeItem
    = "include" UsePath ";"                                               -- simple
    | "include" UsePath "with" "{" ListOf<IncludeNamesItem, ","> "}"     -- aliased

  IncludeNamesItem = ident "as" ident

  UseItem = "use" UsePath "." "{" UseNamesList "}"

  UseNamesList = NonemptyListOf<UseNamesItem, ",">

  UseNamesItem = ident ("as" ident)?

  ExternType
    = ExternTypeFunc
    | ExternTypeInterface

  ExternTypeFunc = FuncType ";"

  ExternTypeInterface = "interface" "{" InterfaceItems* "}"

  ToplevelUseItem = "use" UsePath ("as" ident)? ";"

  UsePath
    = QualifiedUsePath
    | BareUsePath

  BareUsePath = ident

  QualifiedUsePath = packageIdentifier

  InterfaceItem
    = Gate "interface" ident "{" InterfaceItems* "}"

  InterfaceItems = Gate InterfaceDefinition

  InterfaceDefinition
    = TypedefItem
    | UseItem
    | FuncItem

  TypedefItem
    = TypeItem
    | RecordItem
    | EnumItem
    | VariantItem
    | FlagsItems
    | ResourceItem

  Gate
    = GateItem*

  GateItem
    = UnstableGate
    | SinceGate
    | DeprecatedGate

  UnstableGate = "@unstable" "(" FeatureField ")"

  SinceGate = "@since" "(" VersionField ")"

  DeprecatedGate = "@deprecated" "(" VersionField ")"

  FeatureField = "feature" "=" ident

  VersionField = "version" "=" version

  FlagsItems
    = "flags" ident "{" NonemptyListOf<FlagsField, ","> ","? "}"

  FlagsField = ident

  TypeItem
    = "type" ident "=" TypeRef ";"

  VariantItem
    = "variant" ident "{" NonemptyListOf<VariantCase, ","> ","? "}"

  VariantCase
    = ident ("(" TypeRef ")")?

  EnumItem
    = "enum" ident "{" NonemptyListOf<ident, ","> ","? "}"

  EnumCase
    = ident

  RecordItem
    = "record" ident "{" NonemptyListOf<RecordField, ","> ","? "}"

  RecordField
    = ident ":" TypeRef

  ResourceItem
  	= "resource" ident "{" ResourceMethod* "}"

  ResourceMethod
    = ident ":" "static" FuncType ";"   -- static
    | FuncItem                                -- method
    | "constructor" ParamList ";"             -- constructor

  FuncItem = ident ":" FuncType ";"

  FuncType = "async"? "func" ParamList ResultList?

  ResultList = "->" TypeRef

  ParamList = "(" NamedTypeList ")"

  NamedTypeList = ListOf<NamedType, ",">

  NamedType = ident ":" TypeRef

  TypeRef
  	= SimpleType
    | ResultType
    | TupleType
    | OptionType
    | ListType
    | RefType

  RefType = ident

  ResultType = "result" "<" TypeRef ("," TypeRef)? ">"

  TupleType = "tuple" "<" NonemptyListOf<TypeRef, ","> ","? ">"

  OptionType = "option" "<" TypeRef ">"

  ListType = "list" "<" TypeRef ">"

  SimpleType
  	= "s8" | "s16" | "s32" | "s64"
    | "u8" | "u16" | "u32" | "u64"
    | "f32" | "f64"
    | "bool"
    | "string"
    | "char"

  packageIdentifier
  	= (ident ":")+ package ("@" version)?

  version
    = (digit+) "." (digit+) "." (digit+)

  package
  	= ident ("/" ident)*

  ident
    = label

  label
    = fragment ("-" fragment)*

  fragment
    = word
    | acronym

  word
    = lower (lower | digit)*

  acronym
    = upper (upper | digit)*

  space := whitespace | lineComment | multiLineComment
  whitespace = " " | "\t" | "\n" | "\r"  // explicit whitespace chars
  lineComment = "//" (~"\n" any)* "\n"    // single line comments
  multiLineComment = "/*" (~"*/" any)* "*/"  // multi-line comments

}
